plugins {
    alias(libs.plugins.android.application)
}

android {
    compileSdk = libs.versions.compileSdk.get().toInteger()

    namespace = 'com.sshdaemon'

    defaultConfig {
        applicationId "com.daemon.ssh"
        minSdkVersion libs.versions.minSdk.get().toInteger()
        targetSdkVersion libs.versions.targetSdk.get().toInteger()
        versionCode libs.versions.versionCode.get().toInteger()
        versionName libs.versions.versionName.get()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES.txt',
                         'META-INF/LICENSE.txt',
                         'META-INF/NOTICE.txt',
                         'META-INF/NOTICE',
                         'META-INF/LICENSE',
                         'META-INF/DEPENDENCIES',
                         'META-INF/notice.txt',
                         'META-INF/license.txt',
                         'META-INF/dependencies.txt',
                         'META-INF/LGPL2.1',
                         'META-INF/services/javax.annotation.processing.Processor']
        }
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }


    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        unitTests.all {
            useJUnitPlatform()
        }
    }

    lint {
        baseline = file("lint-baseline.xml")
        abortOnError = true
        warningsAsErrors = true
        checkDependencies = true
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:deprecation"
    }
}

dependencies {
    api libs.android.material

    // SSH and crypto libraries using bundles
    implementation libs.bundles.sshd
    implementation libs.bundles.slf4j
    implementation libs.bundles.crypto

    // Testing libraries using bundles
    testImplementation libs.bundles.junit.testing
    testRuntimeOnly libs.bundles.junit.runtime

    // Android testing libraries using bundle
    androidTestImplementation libs.bundles.androidx.testing
}
